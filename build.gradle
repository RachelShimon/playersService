plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id "org.openapi.generator" version "4.3.0"
}

ext {

    jacksonDatabindNullableVersion = '0.2.0'
    swaggerAnnotationsVersion = '1.5.22'

    description = 'Players API'
    generateServerLocation = "$buildDir/generated/server"

}

group = 'intuit'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    //Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    //implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'


    // OpenApi and Swagger generator
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.8'
    implementation "io.swagger:swagger-annotations:${swaggerAnnotationsVersion}"
    implementation "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    // Apache commons
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'commons-io:commons-io:2.4'
    implementation 'commons-codec:commons-codec:1.15'

    //Open CSV
    implementation 'com.opencsv:opencsv:5.6'
}


openApiGenerate {
    // Configuration for generating Java Spring code
    generatorName = "spring"
    inputSpec = "$projectDir/openApi/service.yaml"
    outputDir = "$generateServerLocation"
    apiPackage = "${group}.api.generated"
    invokerPackage = "${group}.api.generated.invoker"
    modelPackage = "${group}.generated.model"
    modelNameSuffix = "Dto"

    configOptions = [
            dateLibrary            : "java8",
            hideGenerationTimestamp: "true",
            interfaceOnly          : "true",
            unhandledException     : "true",
            useTags                : "true"
    ]
}


tasks.named('test') {
    useJUnitPlatform()
}


compileJava.dependsOn tasks.openApiGenerate